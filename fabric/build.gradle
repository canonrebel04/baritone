/*
 * This file is part of Baritone.
 *
 * Baritone is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Baritone is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with Baritone.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id "com.github.johnrengelman.shadow"
    id "xyz.wagyourtail.unimined"
}

archivesBaseName = archivesBaseName + "-fabric"

unimined.minecraft(sourceSets.main) {
    version = rootProject.minecraft_version

    mappings {
        intermediary()
        mojmap()
        devFallbackNamespace "mojmap"
    }

    fabric {
        loader rootProject.fabric_version
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

dependencies {
    implementation project(":core")
    implementation project(":launch")
    shadowCommon project(":core")
    shadowCommon project(":launch")

    add("include", "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}")
    implementation "dev.babbaj:nether-pathfinder:${project.nether_pathfinder_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

// These tasks are added by unimined plugin, we configure them after project is evaluated
afterEvaluate {
    if (tasks.findByName("remapJar")) {
        remapJar {
            inputFile.set shadowJar.archiveFile
            dependsOn shadowJar
            archiveClassifier.set null
        }
    }
}

jar {
    archiveClassifier.set "dev"
}

components.java {
    // Only exclude shadowRuntimeElements if it exists
    afterEvaluate {
        if (configurations.findByName("shadowRuntimeElements")) {
            withVariantsFromConfiguration(configurations.shadowRuntimeElements) {
                skip()
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from rootProject.sourceSets.main.allJava
    from project(":api").sourceSets.main.allJava
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "meteordevelopment"
            artifactId "baritone"
            artifact sourcesJar
        }
    }
}
